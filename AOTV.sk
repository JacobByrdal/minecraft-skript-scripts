# ======================================
# ASPECT OF THE END
# ======================================

options:
  aote.item.name: &b&lASPECT OF THE END
  aote.tp.blocks: 10
  aote.speed.seconds: 30
  aote.speed.level: 3
  aote.cooldown.seconds: 0
  aote.msg.cooldown.seconds: 10


# --------------------------------------
# /giveaote [player]
# --------------------------------------
command /giveaote [<player>]:
  permission: godsitems.aote.give
  trigger:
    if arg-1 is set:
      set {_p} to arg-1
    else:
      set {_p} to player

    set {_item} to diamond sword named "{@aote.item.name}"

    clear {_lore::*}
    add "&7A blade infused with void energy." to {_lore::*}
    add "" to {_lore::*}
    add "&bRIGHT CLICK&7: Teleport &b{@aote.tp.blocks}&7 blocks in" to {_lore::*}
    add "&7the direction you are facing." to {_lore::*}
    add "" to {_lore::*}
    add "&d&lABILITY: INSTANT TRANSMISSION" to {_lore::*}
    add "&7Grants &fSpeed {@aote.speed.level}&7 for &e{@aote.speed.seconds}s&7." to {_lore::*}
    add "&7Speed Cooldown: &e{@aote.speed.seconds}s" to {_lore::*}
    add "&7Cooldown: &e{@aote.cooldown.seconds}s" to {_lore::*}
    set lore of {_item} to {_lore::*}

    give {_item} to {_p}
    send "&aYou have been granted &bASPECT OF THE END&a." to {_p}
    if {_p} is not player:
      send "&aGave &bASPECT OF THE END &ato &e%{_p}%&a." to player





# ======================================
# AOTE ability – TP 8 blocks where youre looking
# ======================================
on right click:
  set {_held} to tool of player
  if {_held} is not set:
    stop
  if name of {_held} is not "{@aote.item.name}":
    stop

  # --- cooldown check ---
  set {_id} to uuid of player
  set {_now} to now
  set {_last} to {aote.last.use.%{_id}%}

  if {_last} is set:
    set {_elapsed} to difference between {_now} and {_last}
    if {_elapsed} < {@aote.cooldown.seconds} seconds:
      set {_left} to {@aote.cooldown.seconds} seconds - {_elapsed}
      send "&bAOTE &7» Ability on cooldown. Wait &e%{_left}%&7." to player
      stop

  # --- fallback TP distances ---
  set {_distances::*} to 10 and 8 and 6 and 4 and 2 and 1

  set {_teleported} to false
  set {_start} to location of player

  loop {_distances::*}:
    set {_end} to location loop-value blocks in front of player

    # --- wall check ---
    set {_blocked} to false
    loop blocks between {_start} and {_end}:
      if loop-block is solid:
        set {_blocked} to true
        stop loop

    if {_blocked} is false:
      # SUCCESS — apply cooldown and teleport
      set {aote.last.use.%{_id}%} to {_now}
      teleport player to {_end}
      set {_teleported} to true
      stop loop

  # --- fail message ---
  if {_teleported} is false:
    send "&cNo valid teleport path found!" to player
    stop

  # --- make sound (and effects) ---
  #apply speed {@aote.speed.level} to player for {@aote.speed.seconds} seconds
  play sound "entity.enderman.teleport" at player

  # --- Apply effect plus cooldown ---
  set {_id} to uuid of player
  set {_now} to now
  set {_lastSpeed} to {aote.speed.last.%{_id}%}

  set {_canSpeed} to true
  if {_lastSpeed} is set:
    set {_elapsedSpeed} to difference between {_now} and {_lastSpeed}
    if {_elapsedSpeed} < 31 seconds:
      set {_canSpeed} to false

  # --- apply speed if allowed ---
  if {_canSpeed} is true:
    apply speed {@aote.speed.level} to player for 30 seconds
    set {aote.speed.last.%{_id}%} to {_now}
  #else:
    #send "&bAOTE &7» Speed still on cooldown!" to player


  # --- prevent ability spam message ---
  set {_msgLast} to {aote.last.msg.%{_id}%}
  set {_sendMsg} to false

  if {_msgLast} is not set:
    set {_sendMsg} to true
  else:
    set {_msgElapsed} to difference between {_now} and {_msgLast}
    if {_msgElapsed} >= {@aote.msg.cooldown.seconds} seconds:
      set {_sendMsg} to true

  if {_sendMsg} is true:
    send "&bAOTE &7» &fInstant Transmission!" to player
    set {aote.last.msg.%{_id}%} to {_now}



# ======================================
# Block renaming and smithing, makeing it unable to turn it into a neitherriteSword
# ======================================
# --- Disable using AOTE in a smithing table ---
on inventory click:
  # Check if this is a smithing table inventory
  if "%event-inventory%" contains "smithing":
    loop all items in event-inventory:
      if loop-item is not air:
        if name of loop-item is "{@aote.item.name}":
          cancel event
          send "&cYou can't use a smithing table with &bASPECT OF THE END&c!" to event-player
          stop


# --- Disable renaming but allow combining ---
on inventory click:
  # Check if this is an anvil inventory (by its name)
  if "%event-inventory%" contains "anvil":
    # result slot of an anvil
    if event-slot is slot 2 of event-inventory:
      # base item in slot 0 must be our AOTE
      if slot 0 of event-inventory is not air:
        if name of slot 0 of event-inventory is "{@aote.item.name}":
          # if result name changed -> rename attempt
          if name of slot 2 of event-inventory is not "{@aote.item.name}":
            cancel event
            send "&cYou can't rename &bASPECT OF THE END&c!" to event-player



